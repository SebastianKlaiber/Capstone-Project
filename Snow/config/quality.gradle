apply plugin: 'checkstyle'
apply plugin: 'findbugs'

check.dependsOn 'checkstyle', 'findbugs', 'lint'

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/sun_checks.xml")
  configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/checkstyle/suppressions.xml").absolutePath
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'
  classpath = files()
}

task findbugs(type: FindBugs, dependsOn: assembleDebug) {
  ignoreFailures = false
  effort = "max"
  reportLevel = "high"
  excludeFilter = new File("${project.rootDir}/config/findbugs/findbugs-filter.xml")
  classes = files("${project.rootDir}/app/build/intermediates/classes")

  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  reports {
    xml.enabled = false
    html.enabled = true
    xml {
      destination "$project.buildDir/reports/findbugs/findbugs.xml"
    }
    html {
      destination "$project.buildDir/reports/findbugs/findbugs.html"
    }
  }

  classpath = files()
}

android {
  lintOptions {
    abortOnError true
    xmlReport false
    htmlReport true
    lintConfig file("${project.rootDir}/config/quality/lint/lint.xml")
    htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
  }
}